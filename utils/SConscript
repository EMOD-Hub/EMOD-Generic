# -*- mode: python; -*-
# This Python script, SConscript, invoked by the SConscript in the upper directory,
#
# 1. builds utils static library
import os
from subprocess import Popen, PIPE
import pdb

Import("env")

if os.sys.platform != 'win32':
    # for getting commit/branch info either get the info directly (if we're building in a repo)
    git_rev_handle = Popen( "git rev-list HEAD --count".encode('utf-8').split(), stdout=PIPE )
    rev = git_rev_handle.stdout.read().decode().strip().strip( '"\'' ).replace(" ","_")

    # or look for and use an externally provided file
    root = Dir('#').abspath
    provided_version_info_file = '{0}/utils/version.provided.txt'.format(root)
    
    if rev  == "" and os.path.exists(provided_version_info_file):
        # not a git repo but a version info file has been provided externally so use the info there
        print("Using commit data supplied from: {}".format(provided_version_info_file))
        with open(provided_version_info_file) as version_file:
            version_lines = version_file.read().rstrip().splitlines()
            if len(version_lines) == 5:
                [localuser, branch, sha1, commit_date, rev] = version_lines
            else:
                os.sys.stderr.write("Unable to parse provided version info in file: {}\n".format(provided_version_info_file))
                os.sys.exit(1)
        git_date = commit_date.replace( ' ', '_' )
    else:
        git_branch_handle = Popen( "git rev-parse --abbrev-ref HEAD".split(), stdout=PIPE )
        branch = git_branch_handle.stdout.read().decode().strip().strip( '"\'' ).replace(" ","_")

        git_sha_handle = Popen( "git log --pretty=format:'%h' -n 1".split(), stdout=PIPE )
        sha1 = git_sha_handle.stdout.read().decode().strip().strip( '"\'' ).replace(" ","_")

        git_date_handle = Popen( "git log -1 --pretty=%ai".encode("utf8").split(), stdout=PIPE )
        git_date = git_date_handle.stdout.read().decode().strip().strip( '"\'' ).replace(' ','_')

    print( branch )
    print( str( sha1 ) )

    if rev == "":
        rev = 0
    if branch == "":
        branch = "unknownbranch"
    if sha1 == "":
        sha1 = "unknownsha1"
    if git_date == "":
        git_date = "unknown_date_time"

    REV_STR = "-DREVISION_NUMBER='" + str(rev) + "'"
    env.Append( CCFLAGS=[ REV_STR ] )

    BR_STR = str('-DSCCS_BRANCH=\'"') + str(branch) + str('(') + str(sha1) + str(')"\'')
    env.Append( CCFLAGS=[ BR_STR ] )

    GIT_DATE_STR = '-DSCCS_DATE=\'"' + git_date + '"\''
    env.Append( CCFLAGS=[ GIT_DATE_STR ] )
    
    USER_STR = '-DBUILDER_NAME=\'"{0}"\''.format(os.environ['USER'])
    env.Append( CCFLAGS=[ USER_STR ] )
else:
    root = Dir('#').abspath
    version_info = '{0}\\scripts\\gitversion.cmd {0}\\utils'.format(root)
    print("Executing '{0}'".format(version_info))
    os.system(version_info)

utilsSrcFiles = [
    "BaseProperties.cpp",
    "BinaryArchiveReader.cpp",
    "BinaryArchiveWriter.cpp",
    "ConfigParams.cpp",
    "Configuration.cpp",
    "Configure.cpp",
    "Coredump.cpp",
    "DistributionBase.cpp",
    "DistributionsConfigurable.cpp",
    "DistributionFactory.cpp",
    "Distributions.cpp",
    "Environment.cpp",
    "EventTrigger.cpp",
    "Exceptions.cpp",
    "FileSystem.cpp",
    "IArchive.cpp",
    "IdmDateTime.cpp",
    "IdmMpi.cpp",
    "InterpolatedValueMap.cpp",
    "ISerializable.cpp",
    "ISupports.cpp",
    "JsonFullReader.cpp",
    "JsonFullWriter.cpp",
    "JsonObjectDemog.cpp",
    "Log.cpp",
    "MathFunctions.cpp",
    "Memory.cpp",
    "NodeProperties.cpp",
    "PiecewiseDistributionBase.cpp",
    "ProgramOptions.cpp",
    "ProgVersion.cpp",
    "Properties.cpp",
    "RANDOM.cpp",
    "Sigmoid.cpp",
    "SimulationEnums.cpp",
    "StatusReporter.cpp",
    "stdafx.cpp",
    "suids.cpp",
    "TBDrugTypeParameters.cpp",
    "TBHIVDrugTypeParameters.cpp",
    "Types.cpp",
    "Timers.cpp",
    "WaningEffect.cpp",
]

utilsLib = env.StaticLibrary('utils', utilsSrcFiles)
